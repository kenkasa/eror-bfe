!=======================================================================
module mod_dcdio
!=======================================================================
  use mod_const
  use mod_util
  implicit none

  ! structures
  !
  type :: s_dcd
    integer(4)           :: dcdinfo(20)
    integer              :: natm  = 0
    integer              :: nstep = 0
    real(8), allocatable :: box(:,:)
    real(8), allocatable :: coord(:,:,:)

    real(4), allocatable :: wrk(:, :)
  end type s_dcd


  ! subroutines
  !
  public  :: read_dcd
  public  :: write_dcd
  public  :: alloc_dcd
  public  :: dealloc_dcd
  public  :: get_natm_from_dcd
  public  :: get_total_step_from_dcd

  public  :: dcd_open
  public  :: dcd_close
  public  :: dcd_read_header
  public  :: dcd_write_header
  public  :: read_dcd_oneframe
  public  :: write_dcd_oneframe


  contains
!-----------------------------------------------------------------------
    subroutine dcd_open(dcdfile, iunit)
!-----------------------------------------------------------------------
      implicit none

      character(len=MaxChar), intent(in)  :: dcdfile
      integer,                intent(out) :: iunit


      call open_file(dcdfile, iunit, frmt = "unformatted")
!
    end subroutine dcd_open
!-----------------------------------------------------------------------
!
!-----------------------------------------------------------------------
    subroutine dcd_close(iunit, dcdfile)
!-----------------------------------------------------------------------
      implicit none

      integer,                intent(in)            :: iunit
      character(len=MaxChar), optional, intent(in)  :: dcdfile


      close(iunit)
!
    end subroutine dcd_close
!-----------------------------------------------------------------------

!-----------------------------------------------------------------------
    subroutine dcd_read_header(iunit, dcd)
!-----------------------------------------------------------------------
      implicit none

      integer,     intent(in)    :: iunit 
      type(s_dcd), intent(inout) :: dcd

      integer                             :: i
      integer(4)                          :: dcdinfo(20), ntitle
      character(len=4)                    :: header
      character(len=80)                   :: title(10)


      read(iunit) header, dcdinfo(1:20)
      read(iunit) ntitle, (title(i), i = 1, ntitle)
      read(iunit) dcd%natm

      dcd%dcdinfo = dcdinfo
      dcd%nstep   = dcdinfo(1)


    end subroutine dcd_read_header 
!-----------------------------------------------------------------------
!
!-----------------------------------------------------------------------
    subroutine dcd_write_header(iunit, dcd)
!-----------------------------------------------------------------------
      implicit none

      integer,                intent(in) :: iunit 
      type(s_dcd),            intent(in) :: dcd

      integer                             :: i
      integer(4)                          :: ntitle
      character(len=4)                    :: header
      character(len=80)                   :: title


      header = 'CORD'
      ntitle = 1
      title  = 'REMARK GENERATED BY ANATRA'
      write(iunit) header, dcd%dcdinfo(1:20)
      write(iunit) ntitle, title
      write(iunit) dcd%natm


    end subroutine dcd_write_header 
!-----------------------------------------------------------------------

!-----------------------------------------------------------------------
    subroutine read_dcd(dcdfile, nstep, dcd)
!-----------------------------------------------------------------------
      implicit none

      character(len=MaxChar), intent(in)  :: dcdfile
      integer,                intent(in)  :: nstep
      type(s_dcd),            intent(out) :: dcd

      integer                             :: i, istep
      integer                             :: iunit
      integer                             :: nstep_dcd
      integer(4)                          :: dcdinfo(20), ntitle
      real(8)                             :: box(6)
      character(len=4)                    :: header
      character(len=80)                   :: title(10)

      real(4), allocatable                :: coord(:, :) 


      iunit = UnitDCD 

      write(iw,*)
      write(iw,'("Read_Dcd> Reading DCD file ", a)') trim(dcdfile)

      open(iunit, file=trim(dcdfile), form='unformatted', status='old')
        read(iunit) header, dcdinfo(1:20)
        read(iunit) ntitle, (title(i), i = 1, ntitle)
        read(iunit) dcd%natm

        dcd%dcdinfo = dcdinfo 
        nstep_dcd   = dcdinfo(1)

        ! check number of snapshots
        !
        if (nstep_dcd >= nstep .and. nstep /= 0) then
          dcd%nstep = nstep
        else if (nstep == 0) then
          dcd%nstep = nstep_dcd
        else if (nstep_dcd < nstep) then
          write(iw,'("Read_Dcd> Error. nstep in ctrl &
                     &is larger than snapshots in dcd")')
          stop
        end if

        ! memory allocation
        !
        call alloc_dcd(dcd%natm, dcd%nstep, dcd)
        allocate(coord(1:3, 1:dcd%natm))

        do istep = 1, dcd%nstep
          read(iunit) box(1:6)
          read(iunit) coord(1, 1:dcd%natm)
          read(iunit) coord(2, 1:dcd%natm)
          read(iunit) coord(3, 1:dcd%natm)
          dcd%box(1, istep) = box(1)
          dcd%box(2, istep) = box(3)
          dcd%box(3, istep) = box(6)

          dcd%coord(1:3, 1:dcd%natm, istep) &
            = coord(1:3, 1:dcd%natm)
        end do

      close(iunit)

      write(iw,'("natm  = ", i10)') dcd%natm
      write(iw,'("nstep = ", i10)') dcd%nstep

      deallocate(coord)      
!
    end subroutine read_dcd
!-----------------------------------------------------------------------
!
!-----------------------------------------------------------------------
    subroutine read_dcd_oneframe(iunit, dcd, istep)
!-----------------------------------------------------------------------
      implicit none

      integer,           intent(in)    :: iunit
      type(s_dcd),       intent(inout) :: dcd
      integer, optional, intent(in)    :: istep

      integer                             :: jstep
      real(8)                             :: box(6)

      !real(4), allocatable                :: coord(:, :) 


      !if (.not. allocated(coord)) then
      !  allocate(coord(1:3, 1:dcd%natm))
      !end if

      if (.not. allocated(dcd%wrk)) then
        allocate(dcd%wrk(1:3, 1:dcd%natm))
      end if

      jstep = 1
      if (present(istep)) &
        jstep = istep

      box = 0.0d0
      read(iunit) box(1:6)
      !read(iunit) coord(1, 1:dcd%natm)
      !read(iunit) coord(2, 1:dcd%natm)
      !read(iunit) coord(3, 1:dcd%natm)
      read(iunit) dcd%wrk(1, 1:dcd%natm)
      read(iunit) dcd%wrk(2, 1:dcd%natm)
      read(iunit) dcd%wrk(3, 1:dcd%natm)

      dcd%box(1, jstep) = box(1)
      dcd%box(2, jstep) = box(3)
      dcd%box(3, jstep) = box(6)

      !dcd%coord(1:3, 1:dcd%natm, jstep) &
      !  = coord(1:3, 1:dcd%natm)
      dcd%coord(1:3, 1:dcd%natm, jstep) &
        = dcd%wrk(1:3, 1:dcd%natm)
!
    end subroutine read_dcd_oneframe
!-----------------------------------------------------------------------

!-----------------------------------------------------------------------
    subroutine write_dcd_oneframe(iunit, istep, dcd)
!-----------------------------------------------------------------------
      implicit none

      integer,     intent(in)    :: iunit
      integer,     intent(in)    :: istep
      type(s_dcd), intent(inout) :: dcd

      real(8)                 :: box(6)
      !real(4), allocatable    :: coord(:, :)


      !if (.not. allocated(coord)) then
      !  allocate(coord(1:3, 1:dcd%natm))
      !end if

      if (.not. allocated(dcd%wrk)) then
        allocate(dcd%wrk(1:3, 1:dcd%natm))
      end if

      box    = 0.0d0
      box(1) = dcd%box(1, istep)
      box(3) = dcd%box(2, istep)
      box(6) = dcd%box(3, istep)
    
      dcd%wrk(1:3, 1:dcd%natm) = dcd%coord(1:3, 1:dcd%natm, istep)
      write(iunit) box(1:6)
      write(iunit) dcd%wrk(1, 1:dcd%natm)
      write(iunit) dcd%wrk(2, 1:dcd%natm)
      write(iunit) dcd%wrk(3, 1:dcd%natm)

!
    end subroutine write_dcd_oneframe
!-----------------------------------------------------------------------

!-----------------------------------------------------------------------
    subroutine write_dcd(dcdfile, dcd)
!-----------------------------------------------------------------------
      implicit none

      character(len=MaxChar), intent(in)  :: dcdfile
      type(s_dcd),            intent(in)  :: dcd

      integer                             :: i, istep
      integer                             :: iunit
      integer(4)                          :: ntitle
      real(8)                             :: box(6)
      character(len=4)                    :: header
      character(len=80)                   :: title

      real(4), allocatable                :: coord(:, :) 


      iunit = UnitDCD

      write(iw,*)
      write(iw,'("Write_Dcd> Writing DCD file ", a)') trim(dcdfile)


      header = 'CORD'
      ntitle = 1
      title  = 'REMARK GENERATED BY ANATRA'
      allocate(coord(1:3, 1:dcd%natm))

      open(iunit, file=trim(dcdfile), form='unformatted', status='replace')
        write(iunit) header, dcd%dcdinfo(1:20)
        write(iunit) ntitle, title
        write(iunit) dcd%natm

        do istep = 1, dcd%nstep

          box(1) = dcd%box(1, istep)
          box(2) = 0.0d0
          box(3) = dcd%box(2, istep)
          box(4) = 0.0d0
          box(5) = 0.0d0
          box(6) = dcd%box(3, istep)

          coord(1:3, 1:dcd%natm) = dcd%coord(1:3, 1:dcd%natm, istep)

          write(iunit) box(1:6)
          write(iunit) coord(1, 1:dcd%natm)
          write(iunit) coord(2, 1:dcd%natm)
          write(iunit) coord(3, 1:dcd%natm)

        end do

      close(iunit)

      deallocate(coord)


    end subroutine write_dcd
!-----------------------------------------------------------------------

!-----------------------------------------------------------------------
    subroutine get_natm_from_dcd(dcdfile, natm)
!-----------------------------------------------------------------------
      implicit none

      character(len=MaxChar), intent(in)  :: dcdfile
      integer,                intent(out) :: natm

      integer                             :: i
      integer                             :: iunit
      integer(4)                          :: dcdinfo(20), ntitle
      character(len=4)                    :: header
      character(len=80)                   :: title(10)


      iunit = UnitDCD

      write(iw,*)
      write(iw,'("Get_Natm_From_DCD> Reading DCD file ", a)') trim(dcdfile)

      open(iunit, file=trim(dcdfile), form='unformatted', status='old')
        read(iunit) header, dcdinfo(1:20)
        read(iunit) ntitle, (title(i), i = 1, ntitle)
        read(iunit) natm
      close(iunit)
!
    end subroutine get_natm_from_dcd 
!-----------------------------------------------------------------------
!
!-----------------------------------------------------------------------
    subroutine get_dcdinfo_from_dcd(dcdfile, dcdinfo)
!-----------------------------------------------------------------------
      implicit none

      character(len=MaxChar), intent(in)  :: dcdfile
      integer(4),             intent(out) :: dcdinfo(20)

      integer                             :: i
      integer                             :: iunit
      integer(4)                          :: ntitle
      character(len=4)                    :: header
      character(len=80)                   :: title(10)


      iunit = UnitDCD

      write(iw,*)
      write(iw,'("Get_Dcdinfo_From_DCD> Reading DCD file ", a)') trim(dcdfile)

      open(iunit, file=trim(dcdfile), form='unformatted', status='old')
        read(iunit) header, dcdinfo(1:20)
      close(iunit)
!
    end subroutine get_dcdinfo_from_dcd 
!-----------------------------------------------------------------------

!-----------------------------------------------------------------------
    subroutine get_total_step_from_dcd(fset, nstep_tot) 
!-----------------------------------------------------------------------
      implicit none

      character(len=MaxChar), intent(in)  :: fset(:)
      integer,                intent(out) :: nstep_tot

      type(s_dcd)            :: dcd 
      integer                :: itraj, ntraj
      integer                :: iunit
      character(len=MaxChar) :: f

      
      ntraj = size(fset(:))

      nstep_tot = 0
      do itraj = 1, ntraj
        f = fset(itraj)

        if (trim(f) /= "") then
          call dcd_open(fset(itraj), iunit)
          call dcd_read_header(iunit, dcd)
          nstep_tot = nstep_tot + dcd%nstep
          call dcd_close(iunit)
        end if
      end do
!
    end subroutine get_total_step_from_dcd
!-----------------------------------------------------------------------


!-----------------------------------------------------------------------
    subroutine alloc_dcd(natm, nstep, dcd)
!-----------------------------------------------------------------------
      implicit none

      integer,     intent(in)    :: natm
      integer,     intent(in)    :: nstep
      type(s_dcd), intent(inout) :: dcd


      allocate(dcd%coord(1:3, 1:natm, 1:nstep), dcd%box(1:3, 1:nstep))


    end subroutine alloc_dcd
!-----------------------------------------------------------------------
!
!-----------------------------------------------------------------------
    subroutine dealloc_dcd(dcd)
!-----------------------------------------------------------------------
      implicit none

      type(s_dcd), intent(inout) :: dcd


      deallocate(dcd%coord, dcd%box)


    end subroutine dealloc_dcd
!-----------------------------------------------------------------------
!
end module mod_dcdio
!=======================================================================
